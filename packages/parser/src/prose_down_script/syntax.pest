keywords = { "eff" | "handle" | "trait" | "impl" | "where" | "data" | "fix" | "const" | "nominal" | "structual" | "forall" | "exists" }
lineComment = { ws* ~ "//" ~ (!br ~ ANY)* ~ (&EOI | br) }

// idents
lowerIdent = _{ !(&keywords) ~ ASCII_ALPHA_LOWER ~ ASCII_ALPHANUMERIC* }
upperIdent = _{ ASCII_ALPHA_UPPER ~ ASCII_ALPHANUMERIC* }
ident = { varIdent | opIdent }
  opIdent = _{ "(" ~ operator ~ ")" }
  varIdent = { lowerIdent }
typeIdent = { existsIdent | forallIdent }
  existsIdent = { upperIdent }
  forallIdent = { lowerIdent }
handlerIdent = { "*" ~ lowerIdent }
instIdent = { "@" ~ lowerIdent }
traitIdent = { upperIdent }

//
defOp = _{ ws* ~ "::" ~ ws* }
assignOp = _{ ws* ~ "=" ~ ws* }
assignArgs = { (ws+ ~ pattern)* }
whereClause = { (ws* ~ "where" ~ ws* ~ stmtsBlock)? }
etaEnvs = { ws* ~ ("<" ~ ws* ~ etaEnvN ~ ws* ~ ","? ~ ">")? }
  etaEnv = { ws* ~ etaHandlerIdent ~ ws* ~ ":" ~ ws* ~ typeExprCoroutine }
    etaHandlerIdent = { lowerIdent }
  etaEnvN = _{ etaEnv ~ ws* ~ ("," ~ ws* ~ etaEnv)* }

// stmts
stmt = { ws* ~ (instTypeDef | handlerTypeDef | assignTypeDef | traitDef | handlerAssign | implAssign | dataAssign | assign) ~ ws* }
instTypeDef = { instIdent ~ defOp ~ typeExpr }
handlerTypeDef = { handlerIdent ~ defOp ~ handlerDefTypeExpr }
assignTypeDef = { ident ~ defOp ~ assignTypeDefExpr }
traitDef = { "trait" ~ ws+ ~ traitConstraintExprs ~ typeIdent ~ traitArgs ~ whereClause }
  traitArgs = { (ws+ ~ typeIdent)* }
  traitValue = _{ stmtsBlock | typeIdent }
  traitExpr = { ws* ~ ((traitValue ~ ws* ~ traitOp ~ ws* ~ traitValue ~ ws*)+ | traitValue) ~ ws* }
  traitOp = { traitOpAnd | traitOpOr }
    traitOpAnd = { "&" }
    traitOpOr = { "|" }
implAssign = { "impl" ~ ws+ ~ traitConstraintExprs ~ existsIdent ~ traitArgs ~ whereClause }
dataAssign = { dataModifier ~ "data" ~ ws+ ~ traitConstraintExprs ~ existsIdent ~ dataArgs ~ ws* ~ "=" ~ ws* ~ dataExpr }
  dataModifier = { ((dataModifierNominal | dataModifierStructual) ~ ws+)? }
    dataModifierNominal = { "nominal" }
    dataModifierStructual = { "structual" }
  dataArgs = { (ws+ ~ typeIdent)* }
  dataExpr = { dataExprOr | dataValue }
    dataExprOr = { dataTerm ~ ws* ~ "|" ~ ws* ~ dataExpr }
    dataTerm = _{ dataValue }
    dataValueGroup = { "(" ~ ws* ~ dataExpr ~ ws* ~ ")" }
    dataValue = { dataValueGroup | dataValueConstructor | dataValueUnit }
      dataValueConstructor = { dataModifier ~ typeIdent ~ dataValueConstructorArgs }
        dataValueConstructorArgs = { (ws+ ~ dataTerm)* }
      dataValueUnit = { "(" ~ ws* ~ ")" }
      // TODO Array

// TODO: dataAssign
handlerAssign = { handlerIdent ~ assignArgs ~ assignOp ~ expr ~ whereClause? }
assign = { ident ~ assignArgs ~ assignOp ~ expr ~ whereClause }

assignTypeDefExpr = { traitConstraintExprs ~ etaEnvs ~ abstructionTypeExpr }
// type exprs
typeExpr = { traitConstraintExprs ~ etaEnvs ~ abstructionTypeExpr }
handlerDefTypeExpr = { traitConstraintExprs ~ etaEnvs ~ abstructionTypeExpr }
coroutineTypeExpr = { traitConstraintExprs ~ typeExprCoroutine }
traitConstraintExprs = { (ws* ~ (traitConstraintGroup | traitConstraintExprN) ~ ws* ~ "=>" ~ ws*)? }
  traitConstraintExpr = { traitIdent ~ (ws+ ~ forallIdent)* }
  // TODO
  traitConstraintExprN = _{ traitConstraintExpr ~ ws* ~ ("," ~ ws* ~ traitConstraintExpr)* }
  traitConstraintGroup = _{ "(" ~ ws* ~ traitConstraintExprN ~ ws* ~ ")" }
typeExprCoroutine = { ws* ~ typeExprCoroutine1 }
  typeExprCoroutine1 = _{ "<" ~ ws* ~ abstructionTypeExpr ~ ws* ~ "," ~ ws* ~ abstructionTypeExpr ~ ws* ~ ">" }
abstructionTypeExpr = { ws* ~ (typeExprArrow | typeExprConstructor | typeExprGroup | typeExprLiteral) }
typeExprArrow = { (typeExprLiteral | typeExprGroup) ~ ws* ~ "->" ~ ws* ~ abstructionTypeExpr }
typeExprConstructor = { typeIdent ~ (ws+ ~ abstructionTypeExpr)* }
typeExprGroup = { "(" ~ ws* ~ abstructionTypeExpr ~ ws* ~ ")" }
typeExprLiteral = { typeExprUnit | typeExprTuple | typeExprArray | typeExprDivergent | typeExprTop | typeExprConstructor | typeExprCoroutine }
  typeExprUnit = { "(" ~ ws* ~ ")" }
  typeExprTuple = { "(" ~ ws* ~ (typeExprTupleN | typeExprTuple1) ~ ws* ~ ")" }
    typeExprTuple1 = _{ abstructionTypeExpr ~ ws* ~ "," }
    typeExprTupleN = _{ abstructionTypeExpr ~ (ws* ~ "," ~ ws* ~ abstructionTypeExpr)+ ~ ws* ~ ","? }
  typeExprArray = { "[" ~ ws* ~ abstructionTypeExpr ~ ws* ~ "]" }
  typeExprDivergent = { "!" }
  typeExprTop = { "_" }

// exprs
expr = { (instApply | effApply | apply | withParen | variable | instIdent | ident | typeIdent | literal) ~ ws* ~ expr? }
// TODO: prefixOperator
// TODO: postfixOperator
infixOperator = { operator | infixIdent }
  operator = { operatorChar ~ (operatorChar | "#" | "@")* }
    operatorChar = _{ "." | "+" | "*" | "-" | "/" | "&" | "|" | ">" | "<" | "^" | "$" | "%" | ":" | "=" | "!" }
  infixIdent = { infixIdentSeparator ~ (!infixIdentSeparator ~ ident) ~ infixIdentSeparator }
    infixIdentSeparator = { "`" }
  literal = { arrayLiteral | tupleLiteral | charLiteral | textLiteral | intLiteral | unitLiteral }
    textLiteral = { "\"" ~ textChar* ~ "\"" }
      textChar = { !"\"" ~ textCharChar }
      textCharChar = _{ (textEscapedChar | ANY) }
        textEscapedChar = _{ "\\" ~ ANY }
    intLiteral = { "0" | (("+" | "-")?  ~ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }
    charLiteral = { "'" ~ (("\\" ~ ANY) | ANY) ~ "'" }
    arrayLiteral = { "[" ~ expr* ~ "]"}
    unitLiteral = { "(" ~ ws* ~ ","? ~ ws* ~ ")" }
    tupleLiteral = { "(" ~ ws* ~ (tupleLiteralN | tupleLiteral1) ~ ws* ~ ")" }
      tupleLiteral1 = _{ expr ~ ws* ~ "," }
      tupleLiteralN = _{ expr ~ (ws* ~ "," ~ ws* ~ expr)+ ~ ws* ~ ","? }
  term = _{ withParen | variable | ident | literal }
  withParen = _{ "(" ~ ws* ~ expr ~ ws* ~ ")" }
  apply = { applyInfix | applyPrefix | applyPrefixOp }
    applyPrefix = { ident ~ (ws+ ~ expr)? }
    applyInfix = { term ~ ws* ~ infixOperator ~ ws* ~ expr }
    applyPrefixOp = { operator ~ expr }
      // TODO: applyInfixLeft
      // TODO: applyInfixRight
    // applyArgs = { (ws+ ~ (exprInfix | withParen | ident | literal))* }
  effApply = { "eff" ~ ws+ ~ handlerIdent ~ (ws+ ~ expr)? }
  instApply = { instIdent ~ (ws+ ~ expr)? }

// patterns
pattern = { patternOr | patternValue }
  patternValue = _{ patternAny | patternListHead | patternIdent | patternConstructor | patternLiteral | patternTuple }
    patternLiteral = { literal }
    patternIdent = { varIdent }
    patternConstructor = { typeExprConstructor }
    patternListHead = { "[" ~ ws* ~ patternValue ~ ws* ~ ":" ~ ws* ~ pattern ~ ws* ~ "]" }
    patternAny = { "_" ~ lowerIdent? }
    patternTuple = { "(" ~ ws* ~ patternValue ~ ("," ~ ws* ~ pattern) ~ ws* ~ ")"}
    // TODO: patternData
  patternOr = { patternValue ~ ws* ~ "|" ~ ws* ~ pattern }

// terms
variable = { ident | referenceSelf | variableN | variable3 | variable2 | variable1 }
  variable1 = _{ "#" }
  variable2 = _{ "##" }
  variable3 = _{ "###" }
  variableN = _{ "#" ~ ASCII_DIGIT+ }
  referenceSelf= _{ "#%" }

stmtsBlock = { ws* ~ "{" ~ ws* ~ stmts ~ ws* ~ "}" ~ ws* }
stmtSep = _{ ";" }
stmts = _{ ws* ~ (stmt | (ws* ~ br) | lineComment | stmtSep)* ~ ws*}
pds = _{ ws* ~ stmts ~ ws* }
root = _{ pds? ~ br* ~ &EOI}
